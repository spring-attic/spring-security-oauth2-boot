buildscript {
	dependencies {
		classpath 'io.spring.gradle:spring-build-conventions:0.0.37'
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
		classpath 'io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.27'
		classpath 'io.spring.nohttp:nohttp-gradle:0.0.5.RELEASE'
	}
	repositories {
		maven { url 'https://plugins.gradle.org/m2/' }
		maven { url 'https://repo.spring.io/plugins-release' }
		maven { url 'https://repo.spring.io/plugins-milestone-local' }
	}
}

repositories {
	mavenCentral()
}

apply plugin: 'io.spring.convention.root'
apply plugin: 'io.spring.nohttp'

group = 'org.springframework.security.oauth.boot'
description = 'Spring Security OAuth2 Auto Configuration'

ext.snapshotBuild = version.contains("SNAPSHOT")
ext.releaseBuild = !version.contains("-")
ext.milestoneBuild = !(snapshotBuild || releaseBuild)

project.tasks.check.dependsOn checkstyleNohttp

nohttp {
	source.exclude "**/build/**"
}

subprojects {
        plugins.withType(JavaPlugin) {
                project.sourceCompatibility='1.8'
        }
	apply plugin: 'io.spring.javaformat'
}

allprojects {
    tasks.withType(Test).configureEach {
        forkEvery = 100
    }

}
